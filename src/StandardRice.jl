module StandardRice

export rice_encode, rice_decode


"""
    unary_encode(input::UInt)::BitVector

Encodes the integer using unary coding: `input` 1s followed by a 0.
"""
function unary_encode(input::UInt)::BitVector
    return BitVector(vcat(fill(true, input), false))
end


"""
    binary_encode(input::UInt, k::UInt)::BitVector

Encodes the integer using k bits in binary (MSB first).
"""
function binary_encode(input::UInt, k::UInt)::BitVector
    bits = BitVector()
    for i in k-1:-1:0
        push!(bits, (input >> i) & 1 == 1)
    end
    return bits
end


"""
    encode_value(input::UInt, k::UInt, encoded::BitVector)

Encodes the given value using Rice compression.
"""
function encode_value(value::UInt, k::UInt, encoded::BitVector)
    append!(encoded, unary_encode(value >> k))
    append!(encoded, binary_encode(value & ((1 << k) -1), k))
end

"""
    unsign(value::Int)::UInt

Converts a positive or negative integer into an unsigned integer for Rice Compression
"""
function unsign(value::Int)::UInt
    return ((value<0) ? ~(value<<1) : (value<<1))
end


"""
    rice_encode(data::Vector{Int})::BitVector

Encodes an array of integers using Rice coding.
Returns a BitVector to optimize space usage.
"""
function rice_encode(data::Vector{Int})::BitVector
    encoded = BitVector()  # Initialize an empty BitVector
    k::UInt = ndigits(data[1], base = 2)

    #Encode the k value
    append!(encoded, unary_encode(k))

    #Encode the initial entry in the BitVector
    encode_value(unsign(data[1]), k, encoded)

    #Encode the differences between adjacent values
    encode_value.(unsign.(data[2:end] - data[1:end-1]), k, Ref(encoded))
    return encoded  # Explicitly convert to BitVector
end


"""
    unary_decode(encoded::BitVector, pos::Int)::Tuple{Int, Int}

Decodes the unary-coded integer starting at `pos`.
Returns the decoded integer and the updated position.
"""
function unary_decode(encoded::BitVector, pos::Int)::Tuple{Int, Int}
    output = 0
    while pos <= length(encoded) && encoded[pos]
        output += 1
        pos += 1
    end
    return output, pos + 1  # Skip the terminating 0
end


"""
    binary_decode(encoded::BitVector, pos::Int, k::Int)::Tuple{Int, Int}

Decodes a k-bit binary integer starting at `pos`.
Returns the decoded integer and the updated position.
"""
function binary_decode(encoded::BitVector, pos::Int, k::Int)::Tuple{Int, Int}
    output = 0
    for _ in 1:k
        output = (output << 1) | (encoded[pos] ? 1 : 0)
        pos += 1
    end
    return output, pos
end


"""
    decode_value(pos::Int, k::Int, encoded::BitVector)::Tuple{Int, Int}

Decodes a value using Rice compression.
"""
function decode_value(pos::Int, k::Int, encoded::BitVector)::Tuple{Int, Int}
    q, pos = unary_decode(encoded, pos)
    r, pos = binary_decode(encoded, pos, k)
    value = q << k | r
    return resign(value), pos
end

"""
    resign(value::Int)::Int

Reverses the unsign function to return a positive or negative integer
"""
function resign(value::Int)::Int
    if ((value & 1) == 0)
		value = value>>1;
	else
		value = ~(value>>1);
    end
    return value
end

"""
    rice_decode(encoded::BitVector, k::Int)::Vector{Int}

Decodes a Rice-coded bit stream into its original integer array.
"""
function rice_decode(encoded::BitVector)::Vector{Int}
    decoded = Int[]
    pos = 1

    #Decode the k value
    k, pos = unary_decode(encoded, pos)

    #Decode the initial value
    initial, pos = decode_value(pos, k, encoded)
    push!(decoded, initial)
    current = initial

    while pos <= length(encoded)
        #Solve for each entry by adding the difference to the previous entry
        diff, pos = decode_value(pos, k, encoded)
        current += diff
        push!(decoded, current)
    end

    return decoded
end

end  # module